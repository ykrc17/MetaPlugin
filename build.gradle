buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.71"
    }
}

apply plugin: "kotlin"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compileOnly([
            gradleApi(),
            "org.jetbrains.kotlin:kotlin-stdlib",
            "com.github.jengelman.gradle.plugins:shadow:2.0.4",
    ])
}

def pluginId = "meta-plugin"
def pluginClass = "com.ykrc17.gradle.MetaPlugin"

jar {
    baseName = "meta-plugin"
    def tmpDir = file("$buildDir/tmp/META-INF")
    doFirst {
        file("$tmpDir/gradle-plugins/${pluginId}.properties").with {
            parentFile.mkdirs()
            write("implementation-class=${pluginClass}")
        }
    }
    metaInf {
        from tmpDir
    }
    doLast {
        delete tmpDir
    }
    doLast {
        copy {
            from "build/libs"
            into "release"
        }
    }
}

/**
 * gradle 3.0  kotlin 1.1
 * gradle 4.5  kotlin 1.2-jdk8
 * gradle 5.0  kotlin 1.3-jdk8
 */
compileKotlin {
    kotlinOptions {
        languageVersion = "1.1"
        apiVersion = "1.1"
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8